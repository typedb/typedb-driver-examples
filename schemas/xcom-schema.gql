define

# Attributes

name sub attribute,
	datatype string;
can-proceed sub attribute,
	datatype boolean;
started sub attribute,
	datatype boolean;
progress sub attribute,
	datatype double;
completed sub attribute,
	datatype boolean;

# Static entities

research-project sub entity,
	has name,
	plays research-task,
	plays required-tech,
	plays research-to-begin;

# event sub entity,
#	has name;

# Dynamic entities

campaign sub entity,
	has name,
	plays campaign-with-tasks;

# Static relations

requirement sub relation,
	abstract,
	relates prerequisite,
	relates outcome,
	plays task;

tech-requirement sub requirement,
	abstract,
	relates required-tech as prerequisite,
	relates outcome;

tech-requirement-to-begin-task sub tech-requirement,
	abstract,
	relates required-tech as prerequisite,
	relates task-to-begin as outcome;

tech-requirement-to-begin-research sub tech-requirement-to-begin-task,
	relates required-tech as prerequisite,
	relates research-to-begin as outcome;

# Dynamic relations

campaign-task sub relation,
	abstract,
	relates campaign-with-tasks,
	relates task,
	has started,
	has progress,
	has can-proceed,
	has completed;

campaign-research-task sub campaign-task,
	relates campaign-with-tasks,
	relates research-task as task;

# Test data (for visualization purposes, move out of this file later)

# insert

# # Static data
# $sectoid_autopsy isa research-project, has name "Sectoid Autopsy";
# $alien_biotech isa research-project, has name "Alien Biotech";
# (required-tech: $alien_biotech, research-to-begin: $sectoid_autopsy) isa tech-requirement-to-begin-research;

# # Dynamic data
# $campaign1 isa campaign, has name "Campaign 1";
# (campaign-with-tasks: $campaign1, research-task: $alien_biotech) isa campaign-research-task, has progress 0;
# (campaign-with-tasks: $campaign1, research-task: $sectoid_autopsy) isa campaign-research-task, has progress 0;

# Rules

## campaign task
task-is-not-started-in-this-campaign-if-progress-is-at-most-0 sub rule,
when {
	$task isa campaign-task, has progress <= 0;
}, then {
	$task has started false;
};

task-is-started-in-this-campaign-if-progress-is-greater-than-0 sub rule,
when {
	$task isa campaign-task, has progress > 0;
}, then {
	$task has started true;
};

task-is-not-completed-in-this-campaign-if-progress-is-less-than-100 sub rule,
when {
	$task isa campaign-task, has progress < 100;
}, then {
	$task has completed false;
};

task-is-completed-in-this-campaign-if-progress-is-at-least-100 sub rule,
when {
	$task isa campaign-task, has progress >= 100;
}, then {
	$task has completed true;
};

task-can-not-proceed-in-this-campaign-if-it-is-completed sub rule,
when {
	$task isa campaign-task, has completed true;
}, then {
	$task has can-proceed false;
};

research-can-not-proceed-in-this-campaign-if-any-prerequisite-techs-are-not-researched sub rule,
when {
	$campaign isa campaign;
	$campaign_task($campaign, task: $task) isa campaign-task;
	(research-to-begin: $task, required-tech: $prerequisite) isa tech-requirement-to-begin-research;
	($campaign, $prerequisite) isa campaign-task, has completed false;
}, then {
	$campaign_task has can-proceed false;
};

research-can-proceed-in-this-campaign-if-it-is-not-completed-and-all-its-prerequisite-techs-are-researched sub rule,
when {
	$campaign isa campaign;
	$campaign_task($campaign, task: $task) isa campaign-task, has completed false;
	not {
		(research-to-begin: $task, required-tech: $prerequisite) isa tech-requirement-to-begin-research;
		($campaign, $prerequisite) isa campaign-task, has completed false;
	};
}, then {
	$campaign_task has can-proceed true;
};

# Queries (for visualization; remove later)

# Fetch all research projects currently available to work on, and their current progress % in Campaign 1
# match
# 	$campaign isa campaign, has name = "Campaign 1";
# 	$research_project isa research-project;
# 	($campaign, research-task: $research_project) isa campaign-research-task, has can-proceed true, has progress $progress;
# get $project, $research_project;
