# Copyright 2021 Vaticle
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

define

identifier sub attribute,
	value string;
naptan-id sub attribute,
	value string;
name sub attribute,
	value string;
lat sub attribute,
	value double;
lon sub attribute,
	value double;
duration sub attribute,
	value double;
distance sub attribute,
	value double;

station sub entity,
	owns naptan-id,
	owns name,
	owns lat,
	owns lon,
	plays route:stop,
	plays tunnel:beginning,
	plays tunnel:end,
	plays route:origin,
	plays route:destination,
	plays zone:contained-station;

tube-line sub entity,
	owns name,
	plays route:route-operator;

route-section sub entity,
	owns identifier,
	owns duration,
	plays route:section,
	plays tunnel:service;

# Try using this sentence to formulate the structure of a relation:
# A <relation> owns a <role> in the form of a <subject> (<subject> is any concept, which could be an entity, relation, or attribute)
# For example: a route owns a stop in the form of a station

route sub relation,
	owns identifier,
	relates section,
	relates stop,
	relates origin,
	relates destination,
	relates route-operator;

tunnel sub relation,
	owns identifier,
	owns distance,
	relates beginning,
	relates end,
	relates service;

zone sub relation,
	owns name,
	relates contained-station;
